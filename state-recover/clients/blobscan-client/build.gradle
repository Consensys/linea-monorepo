import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
  id 'net.consensys.zkevm.kotlin-library-conventions'
}

dependencies {
  implementation(project(':jvm-libs:kotlin-extensions'))
  implementation(project(':jvm-libs:future-extensions'))
  implementation(project(':jvm-libs:http-rest'))
  implementation(project(':jvm-libs:json-rpc'))
  implementation(project(':jvm-libs:vertx-helper'))
  implementation(project(':state-recover:app-core'))
  implementation("io.vertx:vertx-web-client:${libs.versions.vertx}")

  testImplementation "com.github.tomakehurst:wiremock-jre8:${libs.versions.wiremock.get()}"
  testImplementation "org.slf4j:slf4j-api:1.7.30"
  testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:${libs.versions.log4j}"
  testImplementation "org.apache.logging.log4j:log4j-core:${libs.versions.log4j}"
}

sourceSets {
  integrationTest {
    kotlin {
      compileClasspath += sourceSets.main.output
      runtimeClasspath += sourceSets.main.output
    }
    compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath + sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.main.output + sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  }
}

task integrationTest(type: Test) {
  test ->
    description = "Runs integration tests."
    group = "verification"
    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDirs = sourceSets.integrationTest.output.classesDirs

    dependsOn(":localStackComposeUp")
    dependsOn(rootProject.tasks.compileContracts)

    testLogging {
      events TestLogEvent.FAILED,
        TestLogEvent.SKIPPED,
        TestLogEvent.STANDARD_ERROR,
        TestLogEvent.STARTED,
        TestLogEvent.PASSED
      exceptionFormat TestExceptionFormat.FULL
      showCauses true
      showExceptions true
      showStackTraces true
      // set showStandardStreams if you need to see test logs
      showStandardStreams false
    }
}
