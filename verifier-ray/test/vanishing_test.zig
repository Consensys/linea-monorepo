const std = @import("std");
const verifier_ray = @import("verifier_ray");
const fixtures = @import("test_vanishing");

const field = verifier_ray.field.koalabear;
const ext = verifier_ray.field.koalabear_ext;
const protocol = verifier_ray.protocol;
const vanishing = verifier_ray.query.vanishing;
const commitment_mod = verifier_ray.crypto.commitment;

test "vanishing quotient honest scenarios match prover-ray" {
    try std.testing.expect(fixtures.scenarios.len > 0);
    inline for (fixtures.scenarios) |case| {
        const system = case.system;
        var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
        defer arena.deinit();
        const proof = try buildProofData(arena.allocator(), case.honest);
        var coins = try protocol.replay(
            system.round_coin_counts[1..],
            system.round_coin_offsets,
            system.total_round_coins,
            proof.rounds,
        );
        const ctx = protocol.Context{ .all_coins = &coins, .rounds = proof.rounds };
        try vanishing.verify(system, .{
            .ctx = ctx,
            .witness_claims = proof.witness_claims,
            .quotient_claims = proof.quotient_claims,
            .module_sizes = proof.module_sizes,
        });
    }
}

test "vanishing quotient invalid scenarios fail identity" {
    var invalid_case_count: usize = 0;
    inline for (fixtures.scenarios) |case| {
        const invalid = case.invalid orelse continue;
        invalid_case_count += 1;
        const system = case.system;
        var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
        defer arena.deinit();
        const proof = try buildProofData(arena.allocator(), invalid);
        var coins = try protocol.replay(
            system.round_coin_counts[1..],
            system.round_coin_offsets,
            system.total_round_coins,
            proof.rounds,
        );
        const ctx = protocol.Context{ .all_coins = &coins, .rounds = proof.rounds };
        try std.testing.expectError(
            error.QuotientIdentityMismatch,
            vanishing.verify(system, .{
                .ctx = ctx,
                .witness_claims = proof.witness_claims,
                .quotient_claims = proof.quotient_claims,
                .module_sizes = proof.module_sizes,
            }),
        );
    }
    try std.testing.expect(invalid_case_count > 0);
}

test "dynamic vanishing module sizes are required and validated" {
    comptime var dynamic_case_count: usize = 0;
    var wrong_size_failures: usize = 0;
    inline for (fixtures.scenarios) |case| {
        const system = case.system;
        if (system.dynamic_module_count == 0) continue;
        dynamic_case_count += 1;

        var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
        defer arena.deinit();

        const valid = try buildProofData(arena.allocator(), case.honest);
        var coins = try protocol.replay(
            system.round_coin_counts[1..],
            system.round_coin_offsets,
            system.total_round_coins,
            valid.rounds,
        );
        const ctx = protocol.Context{ .all_coins = &coins, .rounds = valid.rounds };

        try vanishing.verify(system, .{ .ctx = ctx, .witness_claims = valid.witness_claims, .quotient_claims = valid.quotient_claims, .module_sizes = valid.module_sizes });

        try std.testing.expectError(
            error.MissingDynamicModuleSize,
            vanishing.verify(system, .{ .ctx = ctx, .witness_claims = valid.witness_claims, .quotient_claims = valid.quotient_claims, .module_sizes = &.{} }),
        );

        var zero_sizes = try arena.allocator().dupe(usize, valid.module_sizes);
        zero_sizes[0] = 0;
        try std.testing.expectError(
            error.InvalidModuleSize,
            vanishing.verify(system, .{ .ctx = ctx, .witness_claims = valid.witness_claims, .quotient_claims = valid.quotient_claims, .module_sizes = zero_sizes }),
        );

        var non_power_sizes = try arena.allocator().dupe(usize, valid.module_sizes);
        non_power_sizes[0] = 7;
        try std.testing.expectError(
            error.InvalidModuleSize,
            vanishing.verify(system, .{ .ctx = ctx, .witness_claims = valid.witness_claims, .quotient_claims = valid.quotient_claims, .module_sizes = non_power_sizes }),
        );

        var wrong_sizes = try arena.allocator().dupe(usize, valid.module_sizes);
        wrong_sizes[0] = if (wrong_sizes[0] == 16) 8 else 16;
        vanishing.verify(system, .{ .ctx = ctx, .witness_claims = valid.witness_claims, .quotient_claims = valid.quotient_claims, .module_sizes = wrong_sizes }) catch |err| {
            if (err == error.QuotientIdentityMismatch) wrong_size_failures += 1 else return err;
        };
    }
    try std.testing.expect(dynamic_case_count > 0);
    // Some constraints may trivially vanish at multiple domain sizes (P(r) = 0,
    // Q(r) = 0 simultaneously), so not every case necessarily produces a
    // mismatch. Assert at least one case does to confirm the check is live.
    try std.testing.expect(wrong_size_failures > 0);
}

test "vanishing verify rejects empty systems before reading coin offsets" {
    try std.testing.expectError(
        error.InvalidRoundCount,
        vanishing.verify(.{ .modules = &.{} }, .{
            .ctx = .{
                .all_coins = &[_]protocol.Coin{},
                .rounds = &[_]protocol.RoundMessage{},
            },
            .witness_claims = &[_]ext.Ext{},
            .quotient_claims = &[_]ext.Ext{},
        }),
    );
}

const ProofData = struct {
    rounds: []const protocol.RoundMessage,
    witness_claims: []const ext.Ext,
    quotient_claims: []const ext.Ext,
    module_sizes: []const usize,
};

fn buildProofData(allocator: std.mem.Allocator, proof: fixtures.VanishingProofView) !ProofData {
    const witness_claims = try allocator.alloc(ext.Ext, proof.witness_claims.len);
    for (proof.witness_claims, 0..) |claim, i| witness_claims[i] = ext.Ext.fromUints(claim);

    const quotient_claims = try allocator.alloc(ext.Ext, proof.quotient_claims.len);
    for (proof.quotient_claims, 0..) |claim, i| quotient_claims[i] = ext.Ext.fromUints(claim);

    const round_cells = try buildRoundCells(allocator, proof);
    const rounds = try buildRounds(allocator, proof, round_cells);

    return .{
        .rounds = rounds,
        .witness_claims = witness_claims,
        .quotient_claims = quotient_claims,
        .module_sizes = proof.module_sizes,
    };
}

fn buildRoundCells(allocator: std.mem.Allocator, proof: fixtures.VanishingProofView) ![]const []const protocol.Scalar {
    const round_cells = try allocator.alloc([]const protocol.Scalar, proof.rounds.len);
    for (proof.rounds, 0..) |round, i| {
        const cells = try allocator.alloc(protocol.Scalar, round.cells.len);
        for (round.cells, 0..) |cell, j| {
            cells[j] = switch (cell) {
                .base => |v| .{ .base = field.Element.init(v) },
                .ext => |v| .{ .ext = ext.Ext.fromUints(v) },
            };
        }
        round_cells[i] = cells;
    }
    return round_cells;
}

fn buildRounds(allocator: std.mem.Allocator, proof: fixtures.VanishingProofView, round_cells: []const []const protocol.Scalar) ![]const protocol.RoundMessage {
    const rounds = try allocator.alloc(protocol.RoundMessage, proof.rounds.len);
    for (proof.rounds, 0..) |round, i| {
        rounds[i] = try buildRoundMessage(allocator, round, round_cells[i]);
    }
    return rounds;
}

fn buildRoundMessage(allocator: std.mem.Allocator, round: fixtures.RuntimeTraceRound, cells: []const protocol.Scalar) !protocol.RoundMessage {
    var column_count: usize = 0;
    for (round.columns) |column| {
        column_count += switch (column) {
            .oracle => |commitments| commitments.len,
            .public_base, .public_ext => 1,
        };
    }

    const columns = try allocator.alloc(protocol.ColumnMessage, column_count);
    var next_column: usize = 0;
    for (round.columns) |column| {
        switch (column) {
            .oracle => |commitments| {
                for (commitments) |commitment| {
                    columns[next_column] = .{ .oracle_commitment = commitment_mod.fromUints(commitment) };
                    next_column += 1;
                }
            },
            .public_base => |values| {
                const converted = try allocator.alloc(field.Element, values.len);
                for (values, 0..) |value, i| converted[i] = field.Element.init(value);
                columns[next_column] = .{ .public_column = .{ .base = converted } };
                next_column += 1;
            },
            .public_ext => |values| {
                const converted = try allocator.alloc(ext.Ext, values.len);
                for (values, 0..) |value, i| converted[i] = ext.Ext.fromUints(value);
                columns[next_column] = .{ .public_column = .{ .ext = converted } };
                next_column += 1;
            },
        }
    }

    return .{ .columns = columns, .cells = cells };
}
