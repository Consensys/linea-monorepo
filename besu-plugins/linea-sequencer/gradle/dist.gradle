import de.undercouch.gradle.tasks.download.Download

/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

def lineaBesuDistTar = new File(new File(buildDir, "tmp"), rootProject.besuFilename)

tasks.register('copyLocalLineaBesu', Copy) {
  onlyIf {
    project.hasProperty('useLocalLineaBesuDir')
  }
  def localLineaBesuDir = "${findProperty('useLocalLineaBesuDir')}".replaceFirst('^~', System.getProperty('user.home'))
  doFirst {
    if (!file(localLineaBesuDir).exists()) {
      throw new GradleException("${localLineaBesuDir} not found")
    }
  }

  from new File("${localLineaBesuDir}/build/distributions/${rootProject.besuFilename}")
  into lineaBesuDistTar.parentFile
}

tasks.register('downloadLatestLineaBesu', Download) {
  onlyIf {
    !project.hasProperty('useLocalLineaBesuDir')
  }
    src rootProject.besuUrl
    dest lineaBesuDistTar
    onlyIfModified true
}

version = project.hasProperty('releaseVersion') ? project.getProperty('releaseVersion') : 'snapshot'

jar {
  dependsOn downloadLatestLineaBesu
  dependsOn copyLocalLineaBesu

  archiveBaseName = distributionIdentifier

  manifest {
    attributes(
      'Specification-Title': archiveBaseName.get(),
      'Specification-Version': rootProject.version,
      'Implementation-Title': archiveBaseName.get(),
      'Implementation-Version': calculateVersion()
    )
  }

 from {
    configurations.runtimeClasspath.filter( {! (it.name =~ /log4j.*\.jar/ )} )
            .collect {it.isDirectory() ? it : zipTree(it) }
  }
  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

// Takes the version, and if -SNAPSHOT is part of it replaces SNAPSHOT
// with the git commit version.
def calculateVersion() {
  String version = rootProject.version
  if (version.endsWith("-SNAPSHOT")) {
    version = version.replace("-SNAPSHOT", "-dev-${getCheckedOutGitCommitHash()}")
  }

  return version
}

static def getCheckedOutGitCommitHash() {
  def hashLength = 8
  "git rev-parse HEAD".execute().text.take(hashLength)
}

tasks.register('distTar', Tar) {
  dependsOn jar
  dependsOn downloadLatestLineaBesu
  dependsOn copyLocalLineaBesu

  from(tarTree(lineaBesuDistTar), {
    eachFile { path = path.replaceFirst(rootProject.besuIdentifier, '') }
    includeEmptyDirs = false
    exclude "**/LICENSE"
  })

  from(configurations.installedJars) {
    into "plugins"
    exclude "**/common*.jar"
  }

  into besuPluginsIdentifier
  archiveBaseName = distributionIdentifier
  compression = Compression.GZIP
  archiveExtension = 'tar.gz'

  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
  duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

tasks.register('installDist', Copy) {
  dependsOn distTar

  from(tarTree(distTar.outputs.getFiles().getSingleFile()), {
    eachFile { path = path.replaceFirst(besuPluginsIdentifier, distributionIdentifier) }
    includeEmptyDirs = false
  })
  into "$buildDir/distributions/install"
  duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

tasks.register('distZip', Zip) {
  dependsOn installDist

  archiveBaseName = distributionIdentifier
  from installDist.outputs.getFiles()
  archiveBaseName
  setArchiveExtension 'zip'
  duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

tasks.register("copyPluginJar") {
  dependsOn installDist

  doLast {
    def copyInto = rootProject.besuPluginDir
    def copyFrom = new File("${project(":sequencer").buildDir}/libs")

    copy {
      from copyFrom.toString()
      into copyInto.toString()
      include '*.jar'
    }
  }
}

tasks.register('deployPlugins', Copy) {
  dependsOn jar
  dependsOn installDist

  from(configurations.installedJars) {
    into new File(distributionIdentifier, "plugins")
    exclude "**/common*.jar"
  }

  into installDist.destinationDir
}
