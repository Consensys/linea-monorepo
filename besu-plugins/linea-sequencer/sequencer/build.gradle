/*
 * Copyright Consensys Software Inc.
 *
 * This file is dual-licensed under either the MIT license or Apache License 2.0.
 * See the LICENSE-MIT and LICENSE-APACHE files in the repository root for details.
 *
 * SPDX-License-Identifier: MIT OR Apache-2.0
 */

plugins {
  id 'java'
  id 'java-library-distribution'
  id 'com.google.protobuf' version '0.9.4'
  alias(libs.plugins.lombok)
  alias(libs.plugins.gradleVersions)
  alias(libs.plugins.dependencyManagement)
  alias(libs.plugins.download)
}

// Define versions for gRPC and Protobuf
ext {
  grpcVersion = '1.70.0' // Align with Besu's BOM
  protobufVersion = '3.24.4'
  protocVersion = protobufVersion
}

group = 'net.consensys.linea.besu.plugin'

def lineaSequencerProject = project(lineaSequencerProjectPath)
apply from: lineaSequencerProject.file("gradle/java.gradle")
apply from: lineaSequencerProject.file("gradle/dependency-management.gradle")
apply from: lineaSequencerProject.file('gradle/common-dependencies.gradle')
apply from: lineaSequencerProject.file("gradle/lint.gradle")

dependencies {
  // annotationProcessor generates the file META-INF/services/org.hyperledger.besu.plugin.BesuPlugin
  annotationProcessor 'com.google.auto.service:auto-service'
  compileOnly 'com.google.auto.service:auto-service'

  api "build.linea:blob-compressor:${libs.versions.blobCompressor.get()}"
  api "build.linea.internal:kotlin:${libs.versions.lineaKotlin.get()}"

  implementation "${besuArtifactGroup}:besu-datatypes"
  implementation "${besuArtifactGroup}:besu-evm"
  implementation "${besuArtifactGroup}:besu-plugin-api"
  implementation "${besuArtifactGroup}.internal:besu-crypto-algorithms"
  implementation "${besuArtifactGroup}.internal:besu-ethereum-api"
  implementation "${besuArtifactGroup}.internal:besu-ethereum-core"
  implementation "${besuArtifactGroup}.internal:besu-ethereum-eth"
  implementation "${besuArtifactGroup}.internal:besu-ethereum-rlp"

  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'

  implementation 'com.github.ben-manes.caffeine:caffeine'

  implementation 'com.google.code.gson:gson'

  implementation "io.consensys.tuweni:tuweni-bytes"
  implementation "io.consensys.tuweni:tuweni-units"
  implementation "io.consensys.tuweni:tuweni-toml"

  implementation 'info.picocli:picocli'

  implementation 'net.consensys.linea.zktracer:arithmetization'

  implementation 'org.hibernate.validator:hibernate-validator'

  // gRPC and Protobuf dependencies for gasless functionality
  implementation "io.grpc:grpc-netty:${grpcVersion}"
  implementation "io.grpc:grpc-protobuf:${grpcVersion}"
  implementation "io.grpc:grpc-stub:${grpcVersion}"
  implementation "io.grpc:grpc-api:${grpcVersion}"
  implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
  implementation 'javax.annotation:javax.annotation-api:1.3.2'

  testImplementation "${besuArtifactGroup}.internal:besu-app"
  testImplementation group: "${besuArtifactGroup}.internal", name: "besu-ethereum-core", classifier: "test-support"

  testImplementation 'org.awaitility:awaitility'
  testImplementation "io.grpc:grpc-testing:${grpcVersion}"
  testImplementation "io.grpc:grpc-core:${grpcVersion}"
  testImplementation "io.grpc:grpc-inprocess:${grpcVersion}"
}

// Protobuf configuration for gRPC code generation
protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protocVersion}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.plugins {
        grpc {}
      }
    }
  }
}

// Configure source sets to include generated protobuf code
sourceSets {
  main {
    java {
      srcDir 'build/generated/source/proto/main/grpc'
      srcDir 'build/generated/source/proto/main/java'
    }
  }
}

apply from: lineaSequencerProject.file("gradle/dist.gradle")

// Simple artifacts task that runs jar and distPlugin
task artifacts {
  group = 'Build'
  description = 'Creates all build artifacts (JAR and distribution ZIP)'
  dependsOn 'jar', 'distPlugin'
}

test {
  useJUnitPlatform()
  // Disable parallel execution for this module due to WireMock test interference
  systemProperty "junit.jupiter.execution.parallel.enabled", "false"
  // Add system property to disable macOS DNS resolver for Netty in tests
  systemProperty 'io.netty.resolver.dns.macos.disabled', 'true'
}
