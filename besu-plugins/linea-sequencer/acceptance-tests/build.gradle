import java.time.LocalTime

/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id 'common-plugins'
  id 'org.web3j' version '4.12.0'
  id 'org.web3j.solidity' version '0.5.0'
}

apply from: rootProject.file("gradle/java.gradle")
apply from: rootProject.file("gradle/dependency-management.gradle")
apply from: rootProject.file('gradle/common-dependencies.gradle')
apply from: rootProject.file("gradle/tests.gradle")
apply from: rootProject.file("gradle/lint.gradle")

web3j {
  generatedPackageName = 'linea.plugin.acc.test.tests.web3j.generated'
}

solidity {
  resolvePackages = false
  optimizeRuns = 1
  // TODO: remove the forced version, when DEV network is upgraded to support latest forks
  version '0.8.19'
}

processTestResources.dependsOn(':acceptance-tests:generateTestContractWrappers')

tasks.register('acceptanceTests', Test) {
  description = 'Runs acceptance tests.'

  inputs.property("integration.date", LocalTime.now()) // so it runs on every invocation

  useJUnitPlatform {
    includeTags("AcceptanceTest")
  }
}

dependencies {
  testImplementation project(':native:compress')

  testImplementation project(':sequencer')

  testImplementation "${besuArtifactGroup}:besu-datatypes"
  testImplementation "${besuArtifactGroup}.internal:api"
  testImplementation "${besuArtifactGroup}.internal:core"
  testImplementation "${besuArtifactGroup}.internal:dsl"
  testImplementation "${besuArtifactGroup}.internal:eth"
  testImplementation "${besuArtifactGroup}.internal:metrics-core"
  testImplementation "${besuArtifactGroup}.internal:services"

  testImplementation 'net.consensys.linea.zktracer:arithmetization'

  testImplementation 'org.awaitility:awaitility'
}

test.enabled = false

jar {
  enabled = false
}
