plugins {
  id "net.consensys.zkevm.kotlin-library-conventions"
  id "net.consensys.zkevm.linea-contracts-helper"
}

dependencies {
  api(project(":coordinator:core"))
  implementation(project(":jvm-libs:generic:persistence:db"))
  implementation(project(":jvm-libs:linea:core:metrics"))
  implementation(project(":coordinator:clients:prover-client:file-based-client")) {
    because "ProverResponseIndex is a part of it"
  }

  testImplementation(project(":coordinator:persistence:db-common"))
  testImplementation(testFixtures(project(":coordinator:core")))
  testImplementation(testFixtures(project(":coordinator:persistence:db-common")))
  testImplementation(testFixtures(project(":jvm-libs:generic:persistence:db")))
  testImplementation(testFixtures(project(":jvm-libs:generic:extensions:kotlin")))
  testImplementation("io.vertx:vertx-junit5")
}

sourceSets {
  integrationTest {
    kotlin {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
    compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath + sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.main.output + sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  }
}

task integrationTest(type: Test) { test ->
  description = "Runs integration tests."
  group = "verification"
  useJUnitPlatform()

  classpath = sourceSets.integrationTest.runtimeClasspath
  testClassesDirs = sourceSets.integrationTest.output.classesDirs

  dependsOn(":localStackComposeUp")
}
