plugins {
  id 'net.consensys.zkevm.kotlin-library-conventions'
  id 'net.consensys.zkevm.linea-contracts-helper'
}

dependencies {
  // TODO: review dependencies
  implementation "io.vertx:vertx-core"
  implementation project(':jvm-libs:vertx-helper')
  implementation project(':jvm-libs:future-extensions')
  implementation project(':jvm-libs:web3j-extensions')
  implementation project(':coordinator:utilities')
  implementation project(':coordinator:core')
  implementation project(':coordinator:clients:smart-contract-client')
  implementation project(':coordinator:ethereum:common')
  implementation project(':coordinator:ethereum:models-helper')
  implementation project(':coordinator:clients:web3signer-client')

  testImplementation "io.vertx:vertx-junit5"
  testImplementation project(':coordinator:ethereum:test-utils')
}

sourceSets {
  integrationTest {
    kotlin {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
    compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath + sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.main.output + sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  }
}

tasks.register('integrationTest', Test) {
  test ->
    description = "Runs integration tests."
    group = "verification"
    useJUnitPlatform()

    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    dependsOn(":localStackComposeUp")
    dependsOn(compileContracts)
}
