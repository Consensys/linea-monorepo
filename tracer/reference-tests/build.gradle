/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id "common-plugins"
}

apply from: rootProject.file("gradle/corset.gradle")
apply from: rootProject.file("gradle/java.gradle")
//apply from: rootProject.file("gradle/tests.gradle")
//apply from: rootProject.file("gradle/trace-files.gradle")
apply from: rootProject.file("gradle/dependency-management.gradle")
apply from: rootProject.file('gradle/common-dependencies.gradle')
apply from: rootProject.file("gradle/lint.gradle")

configurations.configureEach {
  exclude module: 'log4j-api'
  exclude module: 'log4j-core'
  exclude module: 'log4j-slf4j2-impl'
}

tasks.register("generateBlockchainReferenceTests", RefTestGenerationTask) {
  refTestTemplateFilePath = "src/test/resources/templates/BlockchainReferenceTest.java.template"
  refTests = "src/test/resources/ethereum-tests/BlockchainTests"
  refTestJsonParamsDirectory = "BlockchainTests"
  refTestJsonParamsExcludedPath = "BlockchainTests/InvalidBlocks/bcExpectSection" // exclude test for test filling tool
  refTestNamePrefix = "BlockchainReferenceTest"
  generatedRefTestsOutput = "src/test/java/net/consensys/linea/generated/blockchain"
  failedTestsFilePath = System.getenv('FAILED_TEST_JSON_DIRECTORY') ?: "../tmp/local/"
  failedModule = System.getenv('FAILED_MODULE') ?: ""
  failedConstraint = System.getenv('FAILED_CONSTRAINT') ?: ""
}

tasks.register("generateGeneralStateReferenceTests", RefTestGenerationTask) {
  refTestTemplateFilePath = "src/test/resources/templates/GeneralStateReferenceTest.java.template"
  refTests = "src/test/resources/ethereum-tests/GeneralStateTests"
  refTestJsonParamsDirectory = "ethereum-tests/GeneralStateTests"
  refTestJsonParamsExcludedPath = ""
  refTestNamePrefix = "GeneralStateReferenceTest"
  generatedRefTestsOutput = "src/test/java/net/consensys/linea/generated/generalstate"
  failedTestsFilePath = System.getenv('FAILED_TEST_JSON_DIRECTORY') ?: "../tmp/local/"
  failedModule = System.getenv('FAILED_MODULE') ?: ""
  failedConstraint = System.getenv('FAILED_CONSTRAINT') ?: ""
}

tasks.register('referenceBlockchainTests', Test) {
  description = 'Runs ETH reference blockchain tests.'
  dependsOn generateBlockchainReferenceTests
  environment.put("REFERENCE_TEST_OUTCOME_OUTPUT_FILE", "BlockchainReferenceTestOutcome.json")

  boolean isCiServer = System.getenv().containsKey("CI")
  minHeapSize = isCiServer ? "8g" :"4g"
  maxHeapSize = isCiServer ? "32g" : "8g"

  useJUnitPlatform {
    includeTags("BlockchainReferenceTest")
  }
}

tasks.register('referenceGeneralStateTests', Test) {
  description = 'Runs ETH reference general state tests.'
  dependsOn generateGeneralStateReferenceTests

  environment.put("REFERENCE_TEST_OUTCOME_OUTPUT_FILE", "GeneralStateReferenceTestOutcome.json")

  useJUnitPlatform {
    includeTags("GeneralStateReferenceTest")
  }

  boolean isCiServer = System.getenv().containsKey("CI")
  minHeapSize = "4g"
  maxHeapSize = isCiServer ? "32g" : "8g"
}

[
  tasks.named("referenceGeneralStateTests"),
  tasks.named("referenceBlockchainTests"),
].forEach {
  it.configure {
    dependsOn buildReferenceTestsZkevmBin
    environment.put("ZKEVM_BIN", "zkevm_for_reference_tests.bin")

    systemProperty("junit.jupiter.execution.timeout.default", "15 m")  // 5 minutes
    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
    systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
      System.getenv().getOrDefault("REFERENCE_TESTS_PARALLELISM", "1").toInteger())
  }
}

dependencies {
  testImplementation "${besuArtifactGroup}:besu-datatypes"
  testImplementation "${besuArtifactGroup}:evm"
  testImplementation "${besuArtifactGroup}.internal:config"
  testImplementation "${besuArtifactGroup}.internal:core"
  testImplementation "${besuArtifactGroup}.internal:algorithms"
  testImplementation "${besuArtifactGroup}.internal:metrics-core"
  testImplementation "${besuArtifactGroup}.internal:referencetests"
  testImplementation "${besuArtifactGroup}.internal:rlp"
  testImplementation "${besuArtifactGroup}.internal:testutil"
  testImplementation "${besuArtifactGroup}.internal:trie"
  testImplementation "${besuArtifactGroup}.internal:util"
  testImplementation "${besuArtifactGroup}:plugin-api"

  testImplementation 'io.tmio:tuweni-bytes'
  testImplementation 'io.tmio:tuweni-units'

  testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
  testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

  implementation project(":arithmetization")
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
  implementation project(path: ':testing')
  implementation 'org.junit.platform:junit-platform-launcher'
}
