/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id "common-plugins"
  id 'jacoco'
}
jacoco {
  toolVersion = '0.8.12'
}

apply from: rootProject.file("gradle/java.gradle")
apply from: rootProject.file("gradle/dependency-management.gradle")
apply from: rootProject.file('gradle/common-dependencies.gradle')
apply from: rootProject.file("gradle/lint.gradle")

configurations.configureEach {
  exclude module: 'log4j-api'
  exclude module: 'log4j-core'
  exclude module: 'log4j-slf4j2-impl'
}

tasks.register("generateBlockchainReferenceTests", RefTestGenerationTask) {
  refTestTemplateFilePath = "src/test/resources/templates/BlockchainReferenceTest.java.template"
  refTests = "src/test/resources/ethereum-tests/BlockchainTests"
  refTestJsonParamsDirectory = "BlockchainTests"
  refTestJsonParamsExcludedPath = "BlockchainTests/InvalidBlocks/bcExpectSection" // exclude test for test filling tool
  refTestNamePrefix = "BlockchainReferenceTest"
  generatedRefTestsOutput = "src/test/java/net/consensys/linea/generated/blockchain"
  failedTestsFilePath = System.getenv('FAILED_TEST_JSON_DIRECTORY') ?: "../tmp/local/"
  failedModule = System.getenv('FAILED_MODULE') ?: ""
  failedConstraint = System.getenv('FAILED_CONSTRAINT') ?: ""
}

tasks.register('referenceBlockchainTests', Test) {
  finalizedBy(jacocoReferenceBlockchainTestsReport)
  description = 'Runs ETH reference blockchain tests.'
  dependsOn generateBlockchainReferenceTests
  environment.put("REFERENCE_TEST_OUTCOME_OUTPUT_FILE", "BlockchainReferenceTestOutcome.json")

  it.configure {
    // Configure resource limits
    boolean isCiServer = System.getenv().containsKey("CI")
    minHeapSize = "256m"
    maxHeapSize = isCiServer ? "32g" : "8g"
    // Configure parallelism
    environment.put("blockchain", System.getenv("blockchain") ?: "Ethereum")
    systemProperty("junit.jupiter.execution.timeout.default", "15 m")
    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
    systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
            System.getenv().getOrDefault("REFERENCE_TESTS_PARALLELISM", "1").toInteger())
  }

  useJUnitPlatform {
    includeTags("BlockchainReferenceTest")
  }
}

tasks.register("jacocoReferenceBlockchainTestsReport", JacocoReport) {
  reports {
    xml.required = true
  }

  sourceSets project(":arithmetization").sourceSets.main
  executionData(referenceBlockchainTests)
}

dependencies {
  testImplementation "${besuArtifactGroup}:besu-datatypes"
  testImplementation "${besuArtifactGroup}:besu-evm"
  testImplementation "${besuArtifactGroup}:besu-plugin-api"
  testImplementation "${besuArtifactGroup}.internal:besu-config"
  testImplementation "${besuArtifactGroup}.internal:besu-crypto-algorithms"
  testImplementation "${besuArtifactGroup}.internal:besu-ethereum-core"
  testImplementation group: "${besuArtifactGroup}.internal", name: "besu-ethereum-core", classifier: "test-support"
  testImplementation "${besuArtifactGroup}.internal:besu-ethereum-referencetests"
  testImplementation "${besuArtifactGroup}.internal:besu-ethereum-rlp"
  testImplementation "${besuArtifactGroup}.internal:besu-ethereum-trie"
  testImplementation "${besuArtifactGroup}.internal:besu-metrics-core"
  testImplementation "${besuArtifactGroup}.internal:besu-testutil"
  testImplementation "${besuArtifactGroup}.internal:besu-util"

  testImplementation 'io.consensys.tuweni:tuweni-bytes'
  testImplementation 'io.consensys.tuweni:tuweni-units'

  testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
  testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

  implementation project(":arithmetization")
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
  implementation project(path: ':testing')
  implementation 'org.junit.platform:junit-platform-launcher'
}
