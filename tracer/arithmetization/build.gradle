/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id 'java'
  id 'common-plugins'
  id 'com.github.hierynomus.license'
  id "de.undercouch.download"
}

group = 'net.consensys.linea.besu.plugin'
version = rootProject.version

apply from: rootProject.file("gradle/java.gradle")
apply from: rootProject.file("gradle/dependency-management.gradle")
apply from: rootProject.file('gradle/common-dependencies.gradle')
apply from: rootProject.file("gradle/corset.gradle")
apply from: rootProject.file("gradle/tests.gradle")
apply from: rootProject.file("gradle/build-aliases.gradle")
apply from: rootProject.file("gradle/check-licenses.gradle")
apply from: rootProject.file("gradle/lint.gradle")
apply from: rootProject.file("gradle/trace-files.gradle")

dependencies {
  // annotationProcessor generates the file META-INF/services/org.hyperledger.besu.plugin.BesuPlugin
  annotationProcessor 'com.google.auto.service:auto-service'
  compileOnly "${besuArtifactGroup}:evm"
  compileOnly 'com.google.auto.service:auto-service'
  compileOnly 'com.google.auto.service:auto-service-annotations'

  implementation "${besuArtifactGroup}:evm"
  implementation "${besuArtifactGroup}:plugin-api"
  implementation "${besuArtifactGroup}:besu-datatypes"
  implementation "${besuArtifactGroup}.internal:api:${besuVersion}"
  implementation "${besuArtifactGroup}.internal:core:${besuVersion}"
  implementation "${besuArtifactGroup}.internal:rlp:${besuVersion}"
  implementation "${besuArtifactGroup}.internal:algorithms:${besuVersion}"

  implementation 'info.picocli:picocli'

  compileOnly 'io.vertx:vertx-core'

  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

  implementation 'io.tmio:tuweni-bytes'
  implementation 'io.tmio:tuweni-units'
  implementation 'io.tmio:tuweni-toml'
  implementation 'org.bouncycastle:bcprov-jdk18on'
  implementation 'org.hibernate.validator:hibernate-validator'

  testImplementation project(path: ':testing')

  testImplementation "${besuArtifactGroup}:evm"
  testImplementation "${besuArtifactGroup}:besu-datatypes"
  testImplementation "${besuArtifactGroup}.internal:core:${besuVersion}"
  testImplementation "${besuArtifactGroup}.internal:rlp:${besuVersion}"
  testImplementation "${besuArtifactGroup}.internal:core:${besuVersion}"
  testImplementation "${besuArtifactGroup}.internal:referencetests:${besuVersion}"
}

configurations {
  installedJars {
    transitive = false
  }
}

apply from: rootProject.file("gradle/dist.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

tasks.test.configure {
  useJUnitPlatform {
    excludeTags("replay")
  }
}

tasks.register("replayTests", Test) {
  boolean isCiServer = System.getenv().containsKey("CI")
  maxHeapSize = isCiServer ? "32g" : "8g"

  systemProperty("junit.jupiter.execution.parallel.enabled", true)
  systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
  println(System.getenv().get("REPLAY_TESTS_PARALLELISM"))
  systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
    System.getenv().getOrDefault("REPLAY_TESTS_PARALLELISM", "1").toInteger())

  useJUnitPlatform {
    includeTags("replay")
  }
}
