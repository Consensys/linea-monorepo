import org.web3j.solidity.gradle.plugin.EVMVersion
import org.web3j.gradle.plugin.GenerateContractWrappers

buildscript {
  ext {
    shomeiVersion = "${libs.versions.shomei.get()}"
  }
}

plugins {
  id "linea.yul-plugin"
  alias(libs.plugins.besuPluginLibrary)
  alias(libs.plugins.web3j)
  alias(libs.plugins.lombok)
}

def lineaTracerProject = project(lineaTracerProjectPath)
apply from: lineaTracerProject.file("gradle/java.gradle")
apply from: lineaTracerProject.file("gradle/lint.gradle")

dependencies {
  implementation project(path: ':tracer:arithmetization')

  implementation "io.consensys.protocols.shomei:shomei:${shomeiVersion}"
  implementation "io.consensys.protocols.shomei:shomei-server:${shomeiVersion}"
  implementation 'io.micrometer:micrometer-registry-prometheus:1.11.1'
  implementation 'org.assertj:assertj-core'
  implementation 'org.junit.jupiter:junit-jupiter-api'
}

node {
  download = false
}

tasks.withType(GenerateContractWrappers.class).configureEach {
  mustRunAfter(processResources)
}

web3j {
  generatedPackageName = "net.consensys.linea.testing.generated"
}

solidity {
  optimize = false
  prettyJson = true
  evmVersion = EVMVersion.LONDON // TODO: how to make it dynamic ?
}

yul {
  solcVersion = "0.8.26"
  compilerJsonTemplatePath = "src/main/resources/templates/compiler_yul.json.template"
}
