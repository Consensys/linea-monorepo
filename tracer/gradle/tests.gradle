import java.time.LocalTime

/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'jacoco'
jacoco {
  toolVersion = '0.8.12'
}

apply plugin: 'com.adarshr.test-logger'

/*
  * Pass some system properties provided on the gradle command line to test executions for
  * convenience.
  *
  * The properties passed are:
  * - 'test.ethereum.include': allows to run a single Ethereum reference tests. For instance,
  *   running a single general state test can be done with:
  *    ./gradlew :ethereum:tech.pegasys.ethsigner.ethereum.vm:test -Dtest.single=GeneralStateTest -Dtest.ethereum.include=callcodecallcallcode_101-Frontier
  *   The meaning being that will be run only the tests for which the value passed as "include"
  *   (which can be a java pattern) matches parts of the test name. Knowing that tests names for
  *   reference tests are of the form:
  *     <name>(-<milestone>([<variant>])?)?
  *   where <name> is the test name as defined in the json file (usually the name of the json file
  *   as well), <milestone> is the Ethereum milestone tested (not all test use it) and <variant>
  *   is only use in some general state tests where for the same json file and same milestone,
  *   multiple variant of that test are run. The variant is a simple number.
  * - 'root.log.level' and 'evm.log.level': allow to control the log level used during the tests.
  */
tasks.withType(Test).configureEach {
  minHeapSize = "256m"
  maxHeapSize = "8g"

  jvmArgs = [
    '-XX:-UseGCOverheadLimit',
    '-Xlog:gc',
    // Mockito and jackson-databind do some strange reflection during tests.
    // This suppresses an illegal access warning.
    '--add-opens',
    'java.base/java.util=ALL-UNNAMED',
    '--add-opens',
    'java.base/java.util.concurrent=ALL-UNNAMED'
  ]

  Set toImport = [
    'root.log.level',
    'evm.log.level'
  ]
  for (String name : toImport) {
    if (System.getProperty(name) != null) {
      systemProperty name, System.getProperty(name)
    }
  }

  testlogger {
    // pick a theme - mocha, standard, plain, mocha-parallel, standard-parallel or plain-parallel
    theme 'plain-parallel'

    // set to false to disable detailed failure logs
    showExceptions true

    // set to false to hide stack traces
    showStackTraces true

    // set to true to remove any filtering applied to stack traces
    showFullStackTraces true

    // set to false to hide exception causes
    showCauses true

    // set threshold in milliseconds to highlight slow tests
    slowThreshold 2000

    // displays a breakdown of passes, failures and skips along with total duration
    showSummary true

    // set to true to see simple class names
    showSimpleNames false

    // set to false to hide passed tests
    showPassed false

    // set to false to hide skipped tests
    showSkipped false

    // set to false to hide failed tests
    showFailed true

    // enable to see standard out and error streams inline with the test results
    showStandardStreams true

    // set to false to hide passed standard out and error streams
    showPassedStandardStreams false

    // set to false to hide skipped standard out and error streams
    showSkippedStandardStreams false

    // set to false to hide failed standard out and error streams
    showFailedStandardStreams true
  }

  useJUnitPlatform()
}

jacocoTestReport {
  reports {
    xml.required = true
  }
}

tasks.test.configure {
  dependsOn(buildZkevmBin)

  systemProperty("junit.jupiter.execution.parallel.enabled", true)
  systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
  systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
      System.getenv().getOrDefault("UNIT_TESTS_PARALLELISM", "4").toInteger())
  //
  useJUnitPlatform {
    excludeTags("nightly")
    excludeTags("replay")
    excludeTags("weekly")
    excludeTags("prc-calltests")
  }
  finalizedBy(jacocoTestReport)
}

tasks.register("fastReplayTests", Test) {
  dependsOn(buildZkevmBin)
    //
  if (System.getenv().containsKey("REPLAY_TESTS_PARALLELISM")) {
    systemProperty("junit.jupiter.execution.parallel.enabled", true)
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
    systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
      System.getenv().getOrDefault("REPLAY_TESTS_PARALLELISM", "1").toInteger())
  }
  useJUnitPlatform {
    includeTags("replay")
    excludeTags("nightly")
    excludeTags("weekly")
  }
  finalizedBy(jacocoFastReplayTestsReport)
}

tasks.register("jacocoFastReplayTestsReport", JacocoReport) {
  reports {
    xml.required = true
  }

  sourceSets project(":arithmetization").sourceSets.main
  executionData(fastReplayTests)
}

tasks.register("nightlyReplayTests", Test) {
  dependsOn(buildZkevmBin)

  boolean isCiServer = System.getenv().containsKey("CI")
  maxHeapSize = isCiServer ? "32g" : "8g"

  if (System.getenv().containsKey("REPLAY_TESTS_PARALLELISM")) {
    systemProperty("junit.jupiter.execution.parallel.enabled", true)
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
    systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
      System.getenv().getOrDefault("REPLAY_TESTS_PARALLELISM", "1").toInteger())
  }

  useJUnitPlatform {
    includeTags("replay")
    includeTags("nightly")
  }
}

tasks.register("nightlyTests", Test) {
  dependsOn(buildZkevmBin)

  boolean isCiServer = System.getenv().containsKey("CI")
  maxHeapSize = isCiServer ? "32g" : "8g"

  systemProperty("junit.jupiter.execution.parallel.enabled", true)
  systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
  systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
          System.getenv().getOrDefault("NIGHTLY_TESTS_PARALLELISM", "1").toInteger())

  useJUnitPlatform {
    includeTags("nightly")
  }
  finalizedBy(jacocoNightlyTestsReport)
}

tasks.register("jacocoNightlyTestsReport", JacocoReport) {
  reports {
    xml.required = true
  }

  sourceSets project(":arithmetization").sourceSets.main
  executionData(nightlyTests)
}

tasks.register("weeklyTests", Test) {
  dependsOn(buildZkevmBin)

  systemProperty("junit.jupiter.execution.parallel.enabled", true)
  systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
  systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
          System.getenv().getOrDefault("WEEKLY_TESTS_PARALLELISM", "8").toInteger())

  useJUnitPlatform {
    includeTags("weekly")
  }
  finalizedBy(jacocoWeeklyTestsReport)
}

tasks.register("jacocoWeeklyTestsReport", JacocoReport) {
  reports {
    xml.required = true
  }

  sourceSets project(":arithmetization").sourceSets.main
  executionData(weeklyTests)
}

tasks.register("prcCallTests", Test) {
  dependsOn(buildZkevmBin)
  //
  systemProperty("junit.jupiter.execution.parallel.enabled", true)
  systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
  systemProperty("junit.jupiter.execution.parallel.config.strategy", "fixed")
  systemProperty("junit.jupiter.execution.parallel.config.fixed.parallelism",
          System.getenv().getOrDefault("WEEKLY_TESTS_PARALLELISM", "8").toInteger())

  useJUnitPlatform {
    includeTags("prc-calltests")
  }
  finalizedBy(jacocoPrcCallTestsReport)
}

tasks.register("jacocoPrcCallTestsReport", JacocoReport) {
  reports {
    xml.required = true
  }

  sourceSets project(":arithmetization").sourceSets.main
  executionData(prcCallTests)
}

// Gradle task to concatenate Unit, FastReplay, Nightly, Weekly and Reference Blockchain and State tests exec files
tasks.register("jacocoUnitFastReplayWeeklyNightlyReferenceBlockchainAndStateTestsReport", JacocoReport) {
  reports {
    xml.required = true
    html.outputLocation = layout.buildDirectory.dir('reports/jacocoConcatenate/html')
    xml.outputLocation = layout.buildDirectory.file('reports/jacocoConcatenate/jacocoUnitAndReferenceTestsReport.xml')
  }

  sourceSets project(":arithmetization").sourceSets.main
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
}

// Configure default build task to require the tracer is built first.
// This only makes sense for projects which need the tracer.
compileJava.dependsOn 'buildTracer'
