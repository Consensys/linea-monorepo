/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */
import de.undercouch.gradle.tasks.download.Download

def lineaBesuDistTar = new File(new File(buildDir, "downloads"), rootProject.besuFilename)
task downloadLineaBesu {
  outputs.file(lineaBesuDistTar)
  doLast {
    try {
      download.run {
        src rootProject.besuUrl
        dest lineaBesuDistTar
        onlyIfModified true
      }
    } catch (Exception e) {
      def localLineaBesuDir =
              project.hasProperty('useLocalLineaBesuDir')
                      ? file("${findProperty('useLocalLineaBesuDir')}".replaceFirst('^~', System.getProperty('user.home')))
                      : new File(projectDir, "../../linea-besu")

      def localLineaBesuFile = new File("${localLineaBesuDir.canonicalPath}/build/distributions/${rootProject.besuFilename}")

      logger.warn("Could not download " + rootProject.besuUrl + " trying local copy from " + localLineaBesuFile + " as fallback")
      if (!file(localLineaBesuFile).exists()) {
        throw new GradleException("Could not download Linea Besu distribution from: " + rootProject.besuUrl +
                ", and could not find it locally at ${localLineaBesuFile} either")
      }

      copy {
        from localLineaBesuFile
        into lineaBesuDistTar.parentFile
      }
    }
  }
}

task unTarLineaBesu(type: Copy) {
  dependsOn downloadLineaBesu

  from tarTree(lineaBesuDistTar)
  into lineaBesuDistTar.parentFile
}

def lineaBesuLibDir = new File(lineaBesuDistTar.parentFile, rootProject.besuIdentifier + '/lib')
def lineaBesuLibs = []

def excludeBesuProvidedDeps = {
  if(lineaBesuLibs.isEmpty()) {
    // Get all the dependencies that are provided by Besu
    fileTree(dir: lineaBesuLibDir, include: '*.jar').visit {
      FileVisitDetails details ->
        lineaBesuLibs << details.file.name
    }
  }
  // include the dependency in the jar only if it is not already provided by Besu
  !lineaBesuLibs.any { artifactName ->
    if(artifactName == it.name) {
      return true
    }
    // exclude Besu group
    if(it.toString().contains(besuArtifactGroup)) {
      return true
    }
    // try ignoring the version
    def libName = it.name =~ dependencyNamePattern()
    def artName = artifactName =~ dependencyNamePattern()
    libName[0][1] == artName[0][1]
    }
}

jar {
  dependsOn unTarLineaBesu
  archiveBaseName = distributionIdentifier
  version = calculateVersion()

  manifest {
    attributes(
      'Specification-Title': archiveBaseName.get(),
      'Specification-Version': calculateVersion(),
      'Implementation-Title': archiveBaseName.get(),
      'Implementation-Version': calculateVersion()
    )
  }


  from {
    configurations.runtimeClasspath.filter(excludeBesuProvidedDeps).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }

  duplicatesStrategy('exclude')
}

/**
 * Create a distribution of the plugin, that only contains the plugin jar and the
 * dependencies that are not provided by Besu itself, so that is can be simply
 * extracted in the Besu plugins dir.
 */
tasks.register('distPlugin', Zip) {
  dependsOn installDist

  archiveBaseName = distributionIdentifier

  from("${buildDir}/libs/${distributionIdentifier}-${calculateVersion()}.jar")
  from {
    configurations.runtimeClasspath.filter(
            excludeBesuProvidedDeps)

  }
}

static def dependencyNamePattern() {
  /(.*)(\-.*?)\.jar/
}