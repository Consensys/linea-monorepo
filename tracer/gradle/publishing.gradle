/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: "maven-publish"

def cloudsmithUser = project.hasProperty('cloudsmithUser') ? rootProject.property('cloudsmithUser') : System.getenv('CLOUDSMITH_USER')
def cloudsmithKey = project.hasProperty('cloudsmithApiKey') ? rootProject.property('cloudsmithApiKey') : System.getenv('CLOUDSMITH_API_KEY')

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  repositories {
    maven {
      name = "cloudsmith"
      url = "https://api.cloudsmith.io/maven/consensys/linea-arithmetization/"
      credentials {
        username = cloudsmithUser
        password = cloudsmithKey
      }
    }
  }
  publications {
    mavenJava(MavenPublication) {
      groupId "net.consensys.linea.zktracer"
      version "${project.version}"
      from components.java
      artifact sourcesJar

      versionMapping {
        usage('java-api') { fromResolutionOf('runtimeClasspath') }
        usage('java-runtime') { fromResolutionResult() }
      }
      suppressPomMetadataWarningsFor('testFixturesApiElements')
      suppressPomMetadataWarningsFor('testFixturesRuntimeElements')
      pom {
        name = "Linea Arithmetization - ${project.name}"
        url = 'https://github.com/ConsenSys/linea-arithmetization'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/ConsenSys/linea-arithmetization.git'
          developerConnection = 'https://github.com/ConsenSys/linea-arithmetization.git'
          url = 'https://github.com/ConsenSys/linea-arithmetization'
        }
      }
    }
  }
}

tasks.register('cloudsmithUpload') {
  doLast {
    exec {
      executable rootProject.file("scripts/cloudsmith-upload.sh")
      args rootProject.version, project.layout.buildDirectory.get()
    }
  }
}

