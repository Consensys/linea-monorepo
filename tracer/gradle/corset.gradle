/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */
tasks.register('corsetExists') {
    doLast {
        def result = exec{
            ignoreExitValue = true
            commandLine "go-corset", "--version"
        }
        if (result.getExitValue() != 0){
            throw new GradleException('go-corset not found!')
        }
    }
}

// Allowing overriding the default zkevm.bin files.
def ZKEVM_LONDON_BIN = System.getenv("ZKEVM_LONDON_BIN") ?: "zkevm_london.bin"
def ZKEVM_SHANGHAI_BIN = System.getenv("ZKEVM_SHANGHAI_BIN") ?: "zkevm_shanghai.bin"
// Specify where the trace files should be generated.
def TRACE_FILE = "arithmetization/src/main/java/net/consensys/linea/zktracer/Trace.java"
def LONDON_TRACE_FILE = "arithmetization/src/main/java/net/consensys/linea/zktracer/TraceLondon.java"
def SHANGHAI_TRACE_FILE = "arithmetization/src/main/java/net/consensys/linea/zktracer/TraceShanghai.java"
// Specify package to use for trace file
def TRACE_PKG = "net.consensys.linea.zktracer"

tasks.register('buildZkevmBins', Exec) {
    workingDir "${project.rootDir}/linea-constraints/"
    // Specify input files so gradle knows when to rebuild.
    inputs.files(fileTree(workingDir).include('**/*.lisp'))
    // Specify output file so gradle knows when out-of-date.
    outputs.files(fileTree(workingDir).include(ZKEVM_LONDON_BIN))
    outputs.files(fileTree(workingDir).include(ZKEVM_SHANGHAI_BIN))
    // Sanity check executing this makes sense.
    doFirst {
        // sanity check target chain
        def chain = System.getProperty('blockchain')
        // note: null indicates the default (which is Linea).
        if(chain != null && chain != "Linea") {
            println("*** incorrect target chain (expected 'Linea' was '" + chain + "')\n\n")
            assert false
        }
    }
    // Make all forks
    commandLine 'make'
}

// Task which generates the Trace.java interface using go-corset.
tasks.register('buildTracer', Exec) {
    group "Trace.java generation"
    dependsOn 'buildZkevmBins'
    // Locate target file
    def traceJava = getRootProject().file(TRACE_FILE)
    // Configure full path to zkevm_london.bin
    def zkevmLondonBin = getRootProject().file('linea-constraints/' + ZKEVM_LONDON_BIN)
    // Configure full path to zkevm_shanghai.bin
    def zkevmShanghaiBin = getRootProject().file('linea-constraints/' + ZKEVM_SHANGHAI_BIN)
    // Generate trace interface
    commandLine 'go-corset','generate','-p',TRACE_PKG,'-i',traceJava,zkevmLondonBin,zkevmShanghaiBin
}

// Task which generates the TraceLondon.java file using go-corset.
tasks.register('buildLondonTracer', Exec) {
    group "TraceLondon.java generation"
    dependsOn 'buildZkevmBins'
    // Locate target file
    def traceJava = getRootProject().file(LONDON_TRACE_FILE)
    // Configure full path to zkevm.bin
    def zkevmLondonBin = getRootProject().file('linea-constraints/' + ZKEVM_LONDON_BIN)
    // Generate trace class(es)
    commandLine 'go-corset','generate','-p',TRACE_PKG,'-o',traceJava,'-e','Trace',zkevmLondonBin
}

// Task which generates the TraceShanghai.java file using go-corset.
tasks.register('buildShanghaiTracer', Exec) {
    group "TraceLondon.java generation"
    dependsOn 'buildZkevmBins'
    // Locate target file
    def traceJava = getRootProject().file(SHANGHAI_TRACE_FILE)
    // Configure full path to zkevm.bin
    def zkevmShanghaiBin = getRootProject().file('linea-constraints/' + ZKEVM_SHANGHAI_BIN)
    // Generate trace class(es)
    commandLine 'go-corset','generate','-p',TRACE_PKG,'-o',traceJava,'-e','Trace',zkevmShanghaiBin
}

// Task which generates all trace files
tasks.register('buildAllTracers') {
    group "Trace files generation (all forks)"
    dependsOn 'buildTracer'
    dependsOn 'buildLondonTracer'
    dependsOn 'buildShanghaiTracer'
}
