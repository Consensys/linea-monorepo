/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */
tasks.register('corsetExists') {
    doLast {
        def result = exec{
            ignoreExitValue = true
            commandLine "go-corset", "--version"
        }
        if (result.getExitValue() != 0){
            throw new GradleException('go-corset not found!')
        }
    }
}

// Allowing overriding the default zkevm.bin file.
def ZKEVM_BIN = System.getenv("ZKEVM_BIN") ?: "zkevm.bin"
// Allowing overriding the default zkevm.bin file.
def ZKEVM_OLD_REPLAY_BIN = System.getenv("ZKEVM_BIN") ?: "zkevm_for_old_replay_tests.bin"
// Allowing overriding the default zkevm.bin file.
def ZKEVM_REFTESTS_BIN = System.getenv("ZKEVM_BIN") ?: "zkevm_for_reference_tests.bin"
// Specify where the trace file should be generated.
def TRACE_FILE = "arithmetization/src/main/java/net/consensys/linea/zktracer/Trace.java"
// Specify package to use for trace file
def TRACE_PKG = "net.consensys.linea.zktracer"

tasks.register('buildZkevmBin', Exec) {
    workingDir "${project.rootDir}/linea-constraints/"
    // Sanity check executing this makes sense.
    doFirst {    
        // Record zkevm.bin for CorsetValidator
        environment.put("ZKEVM_BIN", ZKEVM_BIN)
        // sanity check target chain
        def chain = System.getProperty('blockchain')
        // note: null indicates the default (which is Linea).
        if(chain != null && chain != "Linea") {
            println("*** incorrect target chain (expected 'Linea' was '" + chain + "')\n\n")
            assert false
        }
    }
    //
    commandLine 'make', ZKEVM_BIN        
    // store the output instead of printing to the console
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register('buildOldReplayTestsZkevmBin', Exec) {
    workingDir "${project.rootDir}/linea-constraints/"
    // Sanity check executing this makes sense.
    doFirst {
        // sanity check target chain
        def chain = System.getProperty('blockchain')
        // note: null indicates the default (which is Linea).
        if(chain != null && chain != "OldLinea") {
            println("*** incorrect target chain (expected 'OldLinea' was '" + chain + "')\n\n")
            assert false
        }
    }
    //
    commandLine 'make', ZKEVM_OLD_REPLAY_BIN
    // Store the output instead of printing to the console
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register('buildReferenceTestsZkevmBin', Exec) {
    workingDir "${project.rootDir}/linea-constraints/"
    // Sanity check executing this makes sense.
    doFirst {
        // sanity check target chain
        def chain = System.getProperty('blockchain')
        //
        if(chain != "Ethereum") {
            println("*** incorrect target chain (expected 'Ethereum' was '" + chain + "')\n\n")
            assert false
        }
    }
    //
    commandLine 'make', ZKEVM_REFTESTS_BIN    
    // Store the output instead of printing to the console
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

// Task which generates the Trace.java file using go-corset.
tasks.register('buildTracer', Exec) {
    group "Trace files generation"

    def chain = System.getProperty('blockchain')
    def zkevmBin = ZKEVM_BIN
    def traceJava = getRootProject().file(TRACE_FILE)    
    // Conditional dependsOn
    if (chain == null) {
        print("No chain provided, building for Linea\n")
        dependsOn 'buildZkevmBin'
    } else if (chain == "Linea") {
        print("Building for Linea\n")
        dependsOn 'buildZkevmBin'
    } else if (chain == "Ethereum") {
        print("Building for Ethereum\n")
        dependsOn 'buildReferenceTestsZkevmBin'
        zkevmBin = ZKEVM_REFTESTS_BIN
    } else if (chain == "OldLinea") {
        print("Building for Old Linea\n")
        dependsOn 'buildOldReplayTestsZkevmBin'
        zkevmBin = ZKEVM_OLD_REPLAY_BIN
    } else {
        // Force build failure if unknown chain.
        println("*** unknown target chain.  Try '-Dblockchain=Linea' or '-Dblockchain=Ethereum'\n\n")
        assert false
    }
    // Configure full path to zkevm.bin
    zkevmBin = getRootProject().file('linea-constraints/' + zkevmBin)
    // Run go-corset generation
    commandLine 'go-corset','generate','-p',TRACE_PKG,'-o',traceJava,zkevmBin
}
