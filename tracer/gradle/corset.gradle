/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */
tasks.register('corsetExists') {
  doLast {
    def result = exec {
      ignoreExitValue = true
      commandLine "go-corset", "--version"
    }
    if (result.getExitValue() != 0) {
      throw new GradleException('go-corset not found!')
    }
  }
}

ext {
  corsetVersion = project.hasProperty('corsetVersion')
    ? project.property('corsetVersion')
    : System.getenv().getOrDefault('CORSET_VERSION', "v1.1.31")
}

tasks.register('installGoCorset') {
  description = 'Install go-corset'
  group = 'build'

  doLast {
    def corsetVersion = project.ext.corsetVersion
    def isWindows = System.getProperty('os.name').toLowerCase().contains('win')
    logger.lifecycle("Target version: $corsetVersion")
    if (corsetVersion.isEmpty()) {
      throw new GradleException("corsetVersion property is not set")
    }

    // Helper function to execute commands
    def executeCommand = { String cmd ->
      def process
      if (isWindows) {
        process = ["cmd", "/c", cmd].execute()
      } else {
        process = cmd.execute()
      }
      return process
    }

    // Check if go-corset exists
    def checkCmd = isWindows ? "where go-corset" : "which go-corset"
    def goCorsetExists = false

    try {
      def checkProcess = executeCommand(checkCmd)
      goCorsetExists = (checkProcess.waitFor() == 0)
    } catch (Exception e) {
      logger.warn("Check command failed: ${e.message}")
      goCorsetExists = false
    }

    def installRequired = true

    if (goCorsetExists) {
      logger.lifecycle("go-corset found. Checking version...")

      try {
        def versionProcess = executeCommand("go-corset --version")
        def out = new StringBuilder()
        def err = new StringBuilder()
        versionProcess.consumeProcessOutput(out, err)
        def exitCode = versionProcess.waitFor()

        if (exitCode == 0) {
          def versionOutput = out.toString().trim()
          logger.lifecycle("Version output: $versionOutput")

          if (versionOutput.contains(corsetVersion)) {
            logger.lifecycle("✓ Correct version ($corsetVersion) already installed")
            installRequired = false
          } else {
            logger.lifecycle("⚠ Version mismatch detected: installed=$versionOutput expected=$corsetVersion")
          }
        } else {
          def errorOutput = err.toString().trim()
          if (errorOutput) {
            logger.warn("Version check error: $errorOutput")
          }
        }
      } catch (Exception e) {
        logger.warn("Could not check version: ${e.message}")
      }
    } else {
      logger.lifecycle("go-corset not found in PATH")
    }

    if (installRequired) {
      logger.lifecycle("Installing go-corset@$corsetVersion...")

      def installCmd = "go install github.com/consensys/go-corset/cmd/go-corset@${corsetVersion}"

      try {
        def installProcess = executeCommand(installCmd)
        // Capture output
        def out = new StringBuilder()
        def err = new StringBuilder()
        installProcess.consumeProcessOutput(out, err)
        def exitCode = installProcess.waitFor()
        if (exitCode == 0) {
          logger.lifecycle("✓ Installation completed successfully: go-corset@$corsetVersion is ready to use")
        } else {
          def errorMsg = err.toString().isEmpty() ? out.toString() : err.toString()
          throw new GradleException(
            "Installation failed with exit code $exitCode:\n$errorMsg"
          )
        }
      } catch (Exception e) {
        throw new GradleException("Installation error: ${e.message}")
      }
    }
  }
}

// Allowing overriding the default zkevm.bin files.
def ZKEVM_OSAKA_BIN = System.getenv("ZKEVM_OSAKA_BIN") ?: "zkevm_osaka.bin"

// Specify package to use for trace file
def TRACE_PKG = "net.consensys.linea.zktracer"

tasks.register('buildZkevmBins', Exec) {
  dependsOn('installGoCorset')
  dependsOn('corsetExists')
  workingDir "${project.rootDir}/tracer/linea-constraints/"
  // Specify input files so gradle knows when to rebuild.
  inputs.files(fileTree(workingDir).include('**/*.lisp'))
  // Specify output file so gradle knows when out-of-date.
  outputs.files(fileTree(workingDir).include(ZKEVM_OSAKA_BIN))
  // Sanity check executing this makes sense.
  doFirst {
    // sanity check target chain
    def chain = System.getProperty('blockchain')
    // note: null indicates the default (which is Linea).
    if (chain != null && chain != "Linea") {
      println("*** incorrect target chain (expected 'Linea' was '" + chain + "')\n\n")
      assert false
    }
  }
  // Make all forks
  commandLine 'make'
}

def lineaTracerProject = project(lineaTracerProjectPath)

// Task which generates the Trace.java interface using go-corset.
tasks.register('buildAllTracers', Exec) {
  group "Trace file(s) generation"
  dependsOn 'buildZkevmBins'
  // Specify where the trace files should be generated.
  def GENERATED_SRC_DIR = "${project.layout.buildDirectory.get()}/generated/gocorset/main/java/net/consensys/linea/zktracer"
  def TRACE_FILE = "$GENERATED_SRC_DIR/Trace.java"
  def OSAKA_TRACE_FILE = "$GENERATED_SRC_DIR/TraceOsaka.java"

  // Locate target files
  def traceJava = lineaTracerProject.file(TRACE_FILE)
  def traceOsakaJava = lineaTracerProject.file(OSAKA_TRACE_FILE)
  // Configure full paths for zkevm.bin files
  def zkevmOsakaBin = lineaTracerProject.file('linea-constraints/' + ZKEVM_OSAKA_BIN)
  // Generate trace interface and implementing classes
  doFirst {
    traceJava.parentFile.mkdirs()
  }
  commandLine 'go-corset', 'generate', '-p', TRACE_PKG, '-i', traceJava,
    '-o', traceOsakaJava, zkevmOsakaBin
}
