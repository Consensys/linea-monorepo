plugins {
  id 'java-library'
  id 'linea.web3j-codegen'
}
java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}
description = 'Web3J Java client for Linea L1 Rollup Smart Contract'
ext.artifactId = 'l1-rollup-contract-client'
group=rootProject.group

dependencies {
  // this shall be very lightweight dependency, just export the generated sources
  compileOnly("org.web3j:core:${libs.versions.web3j.get()}") {
    exclude group: 'org.slf4j', module: 'slf4j-nop'
  }
}

web3jContractWrappers {
  def contractsDir = layout.buildDirectory.dir("${rootProject.projectDir}/contracts/abi").get()
  def contractV6Abi = contractsDir.file("LineaRollupV6.0.abi").asFile.absolutePath
  def contractV5Abi = contractsDir.file("LineaRollupV5.0.abi").asFile.absolutePath

  contractsPackage = "build.linea.contract"
  contracts = [
    "$contractV5Abi": "LineaRollupV5",
    "$contractV6Abi": "LineaRollupV6"
  ]
}
