// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.33;

import { LineaRollupBase } from "./LineaRollupBase.sol";
import { Eip4844BlobAcceptor } from "./dataAvailability/Eip4844BlobAcceptor.sol";
import { ClaimMessageV1 } from "../messaging/l1/v1/ClaimMessageV1.sol";
import { AccessControlUpgradeable } from "@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol";
import { LivenessRecovery } from "./LivenessRecovery.sol";
import { IGenericErrors } from "../interfaces/IGenericErrors.sol";
import { IAddressFilter } from "./forcedTransactions/interfaces/IAddressFilter.sol";
import { LineaRollupYieldExtension } from "./LineaRollupYieldExtension.sol";
/**
 * @title Contract to manage cross-chain messaging on L1, L2 data submission, and rollup proof verification.
 * @author ConsenSys Software Inc.
 * @custom:security-contact security-report@linea.build
 */
contract LineaRollup is
  LineaRollupBase,
  LineaRollupYieldExtension,
  LivenessRecovery,
  Eip4844BlobAcceptor,
  ClaimMessageV1
{
  /// @custom:oz-upgrades-unsafe-allow constructor
  constructor() {
    _disableInitializers();
  }

  /**
   * @notice Initializes LineaRollup and underlying service dependencies - used for new networks only.
   * @dev DEFAULT_ADMIN_ROLE is set for the security council.
   * @dev OPERATOR_ROLE is set for operators.
   * @dev Note: This is used for new testnets and local/CI testing, and will not replace existing proxy based contracts.
   * @param _initializationData The initial data used for contract initialization.
   * @param _livenessRecoveryOperator The liveness recovery operator address.
   * @param _yieldManager The yield manager address.
   */
  function initialize(
    BaseInitializationData calldata _initializationData,
    address _livenessRecoveryOperator,
    address _yieldManager
  ) external initializer {
    bytes32 genesisShnarf = _computeShnarf(
      EMPTY_HASH,
      EMPTY_HASH,
      _initializationData.initialStateRootHash,
      EMPTY_HASH,
      EMPTY_HASH
    );

    _blobShnarfExists[genesisShnarf] = SHNARF_EXISTS_DEFAULT_VALUE;

    __LineaRollup_init(_initializationData, genesisShnarf);
    __LivenessRecovery_init(_livenessRecoveryOperator);
    __LineaRollupYieldExtension_init(_yieldManager);
  }

  /**
   * @notice Revokes `role` from the calling account.
   * @dev Liveness recovery operator cannot renounce role. Reverts with OnlyNonLivenessRecoveryOperator.
   * @param _role The role to renounce.
   * @param _account The account to renounce - can only be the _msgSender().
   */
  function renounceRole(
    bytes32 _role,
    address _account
  ) public virtual override(AccessControlUpgradeable, LivenessRecovery) {
    super.renounceRole(_role, _account);
  }

  /**
   * @notice Sets forced transaction gateway and reinitializes the last finalized state including forced tx data.
   * @dev This function is a reinitializer and can only be called once per version. Should be called using an upgradeAndCall transaction to the ProxyAdmin.
   * @param _forcedTransactionGateway The address of the forced transaction gateway.
   * @param _forcedTransactionFeeInWei The forced transaction fee in wei.
   * @param _addressFilter The address of the address filter.
   */
  function reinitializeLineaRollupV9(
    address _forcedTransactionGateway,
    uint256 _forcedTransactionFeeInWei,
    address _addressFilter
  ) external reinitializer(9) {
    // TODO - ADD PROXY ADMIN CHECK AND FIX TESTS

    require(_forcedTransactionGateway != address(0), IGenericErrors.ZeroAddressNotAllowed());
    require(_forcedTransactionFeeInWei > 0, IGenericErrors.ZeroValueNotAllowed());
    require(_addressFilter != address(0), IGenericErrors.ZeroAddressNotAllowed());


    // TODO - remove this as it will only be done when going live in another transaction.
    _grantRole(FORCED_TRANSACTION_SENDER_ROLE, _forcedTransactionGateway);

    forcedTransactionFeeInWei = _forcedTransactionFeeInWei;
    addressFilter = IAddressFilter(_addressFilter);

    emit ForcedTransactionFeeSet(_forcedTransactionFeeInWei);
    emit AddressFilterChanged(address(0), _addressFilter);

    nextForcedTransactionNumber = 1;

    emit LineaRollupVersionChanged(bytes8("7.1"), bytes8("8"));
  }
}
