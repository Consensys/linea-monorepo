;; Shift a u256 word a given number of bytes to the left.  To shift by
;; n bytes, this operation makes n calls.
fn byte_shl256(word u256, n u5) -> (res u256) {
  var head u8
  var tail u248
  var b u1
  var m u5
  ;; check for base case
  if n == 0 goto base
  ;; split of leading byte
  head,tail = word
  ;; decrement counter
  b,m = n - 1
  res = byte_shl256(256*tail,m)
  return
base:
  res = word
  return
}

;; Shift a u256 word a given number of bytes to the right.  To shift
;; by n bytes, this operation makes n calls.
fn byte_shr256(word u256, n u5) -> (res u256) {
  var head u8
  var tail u248
  var b u1
  var m u5
  ;; check for base case
  if n == 0 goto base
  ;; split of trailing byte
  tail,head = word
  ;; decrement counter
  b,m = n - 1
  res = byte_shr256(tail,m)
  return
base:
  res = word
  return
}

;; Shift an i256 (i.e. signed) word a given number of bytes to the
;; right.  To shift by n bytes, this operation makes n calls.  Since
;; the word is signed, this extends the sign bit accordingly.
fn byte_sar256(word u256, n u5) -> (res u256) {
  var head u8
  var tail u247
  var sign,b u1
  var m u5
  ;; check for base case
  if n == 0 goto base
  ;; split of trailing byte
  sign,tail,head = word
  ;; decrement counter
  b,m = n - 1
  res = byte_sar256((sign * 0xFF80 * 256^30) + tail, m)
  return
base:
  res = word
  return
}

;; Shift a u512 word a given number of bytes to the left.  To shift by
;; n bytes, this operation makes n calls.
fn byte_shl512(word u512, n u6) -> (res u512) {
  res = 0
  return
}

